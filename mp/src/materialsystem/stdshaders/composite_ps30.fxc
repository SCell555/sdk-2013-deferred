
// STATIC:		"ALPHATEST"			"0..1"
// STATIC:		"TRANSLUCENT"		"0..1"
// STATIC:		"READNORMAL"		"0..1"
// STATIC:		"NOCULL"			"0..1"
// STATIC:		"PHONGFRESNEL"		"0..1"
// STATIC:		"ENVMAP"			"0..1"
// STATIC:		"ENVMAPMASK"		"0..1"
// STATIC:		"ENVMAPFRESNEL"		"0..1"

// STATIC:		"RIMLIGHT"						"0..1"
// STATIC:		"RIMLIGHTMODULATELIGHT"			"0..1"

// STATIC:		"BASETEXTURE2"		"0..1"
// STATIC:		"BLENDMODULATE"		"0..1"

// STATIC:		"SELFILLUM"					"0..1"
// STATIC:		"SELFILLUM_MASK"			"0..1"
// STATIC:		"SELFILLUM_ENVMAP_ALPHA"	"0..1"

// STATIC:		"PARALLAXCORRECT"			"0..1"

// DYNAMIC:		"PIXELFOGTYPE"		"0..1"

// SKIP:		$ALPHATEST && $TRANSLUCENT
// SKIP:		!$ENVMAP && $ENVMAPFRESNEL
// SKIP:		!$ENVMAP && $ENVMAPMASK

// SKIP:		$ENVMAP && !$READNORMAL
// SKIP:		$ENVMAPFRESNEL && !$READNORMAL
// SKIP:		$PHONGFRESNEL && !$READNORMAL
// SKIP:		$RIMLIGHT && !$READNORMAL
// SKIP:		!$RIMLIGHT && $RIMLIGHTMODULATELIGHT

// SKIP:		!$BASETEXTURE2 && $BLENDMODULATE
// SKIP:		$MODEL && $BASETEXTURE2

// SKIP:		!$ENVMAP && $PARALLAXCORRECT
// SKIP:		$MODEL && $PARALLAXCORRECT

// SKIP:		!$SELFILLUM && $SELFILLUM_MASK
// SKIP:		!$SELFILLUM && $SELFILLUM_ENVMAP_ALPHA
// SKIP:		$SELFILLUM_MASK && $SELFILLUM_ENVMAP_ALPHA
// SKIP:		$SELFILLUM_MASK && $ENVMAPMASK
// SKIP:		$SELFILLUM_ENVMAP_ALPHA && !$ENVMAPMASK

#include "common_ps_fxc.h"
#include "common_deferred_fxc.h"
#include "common_vertexlitgeneric_dx9.h"


sampler sAlbedo							: register( s0 );

#if ALPHATEST
const float g_flAlphaRef				: register( c0 );
#endif


#if READNORMAL
sampler sNormals						: register( s1 );
#endif

sampler sLightAccum						: register( s2 );

const float2 g_vecFullScreenTexel		: register( c1 );
const float4 g_vecFogParams				: register( c2 );
const float3 g_vecOrigin				: register( c3 );

const float g_flPhongScale				: register( c4 );

#if ENVMAP
sampler sEnvmap								: register( s3 );
const float3 g_flEnvmapTint					: register( c5 );
const float2 g_flEnvmapSaturationContrast	: register( c6 );

#	if ENVMAPMASK
sampler sEnvmapMask							: register( s4 );
#	endif

#	if PARALLAXCORRECT
const float3 cubemapPos						: register( c11 );
const float4x4 obbMatrix					: register( c12 );
#	endif

#endif

#if BASETEXTURE2
sampler sAlbedo2							: register( s5 );
#endif

#if BLENDMODULATE
sampler sBlendmodulate						: register( s6 );
#endif

#if PHONGFRESNEL || ENVMAPFRESNEL
const float3 g_flFresnelRanges				: register( c7 );
#endif

#if RIMLIGHT
const float3 g_flRimLight_Tint						: register( c8 );
const float2 g_flRimLight_Exponent_AlbedoScale		: register( c9 );
#endif

#if ENVMAPMASK && BASETEXTURE2
sampler sEnvmapMask2						: register( s7 );
#endif

#if SELFILLUM
const float3 g_flSelfIllum_Tint				: register( c10 );
#if SELFILLUM_MASK
sampler sSelfIllumMask						: register( s4 ); // override envmap mask
#endif
#endif

struct PS_INPUT
{
#if BLENDMODULATE
	float4 vTexCoord				: TEXCOORD0;
#else
	float2 vTexCoord				: TEXCOORD0;
#endif

	//float3 vProjPosXYW				: TEXCOORD1;
	float3 vWorldPos				: TEXCOORD2;

#if ENVMAP || RIMLIGHT || PHONGFRESNEL || ENVMAPFRESNEL
	float3 worldToEye				: TEXCOORD3;
#endif

#if BASETEXTURE2
	float4 vColor_0					: COLOR0;
#endif

#if NOCULL
	float vDir						: VFACE;
#endif

	float2 vScreenPos				: VPOS;
};


struct PS_OUTPUT
{
	float4 vColor_0				:	COLOR0;
};


PS_OUTPUT main( const PS_INPUT In )
{
	PS_OUTPUT Out;

	float4 flAlbedoSample = tex2D( sAlbedo, In.vTexCoord.xy );

#if BASETEXTURE2
	float4 flAlbedoSample2 = tex2D( sAlbedo2, In.vTexCoord.xy );
	float flBlendfactor = In.vColor_0.w;
#if BLENDMODULATE
	flBlendfactor = GetModulatedBlend( flBlendfactor, tex2D( sBlendmodulate, In.vTexCoord.zw ).rg );
#endif
	flAlbedoSample = lerp( flAlbedoSample, flAlbedoSample2, flBlendfactor );
#endif

#if ALPHATEST
	clip( flAlbedoSample.a - g_flAlphaRef );
#endif

#if TRANSLUCENT
	Out.vColor_0.w = flAlbedoSample.a;
#else
	Out.vColor_0.w = 1;
#endif

	//float2 screenPos = GetLightAccumUVs( In.vProjPosXYW, g_vecHalfScreenTexel );
	float2 screenPos = ( In.vScreenPos + 0.5f ) * g_vecFullScreenTexel;

	float4 flLighting = ReadLighting( tex2D( sLightAccum, screenPos ) );

#if READNORMAL
	float3 worldNormal = tex2D( sNormals, screenPos ).xyz * 2.0f - 1.0f;
#endif


#if RIMLIGHT || PHONGFRESNEL || ENVMAPFRESNEL
	float3 worldToEye = normalize( In.worldToEye );
	worldNormal = normalize( worldNormal );
#endif

#if PHONGFRESNEL || ENVMAPFRESNEL
	float flFresnel = saturate( Fresnel( worldNormal, worldToEye, g_flFresnelRanges ) );
#endif

#if PHONGFRESNEL
	flLighting.w *= flFresnel;
#endif

	float3 flSpecularLighting = (float3)0;

#if RIMLIGHT
	float dotEyeVec = 1.0f - saturate( dot( worldNormal, worldToEye ) );

	float3 rimLighting = pow( dotEyeVec, g_flRimLight_Exponent_AlbedoScale.x );
	rimLighting *= lerp( 1, flAlbedoSample.rgb, g_flRimLight_Exponent_AlbedoScale.y );
	rimLighting *= g_flRimLight_Tint;
#if RIMLIGHTMODULATELIGHT
	rimLighting *= dot( flLighting.rgb, float3( 0.299f, 0.587f, 0.114f ) );
#endif

	flSpecularLighting += rimLighting;
#endif


#if ENVMAP
	float flSpecularFactor;
#if ENVMAPMASK
	float4 vecEnvmapmaskColor = tex2D( sEnvmapMask, In.vTexCoord.xy );
#if BASETEXTURE2
	vecEnvmapmaskColor = lerp( vecEnvmapmaskColor,
		tex2D( sEnvmapMask2, In.vTexCoord.xy ),
		flBlendfactor );
#endif
	flSpecularFactor = vecEnvmapmaskColor.r;
#else
	flSpecularFactor = flAlbedoSample.a;
#endif

#if RIMLIGHT
	float3 reflectVect = reflect( worldToEye, worldNormal );
#else
	float3 reflectVect = CalcReflectionVectorUnnormalized( worldNormal, In.worldToEye );
#endif

#if PARALLAXCORRECT
	float3 positionLS = mul(float4(In.vWorldPos, 1), obbMatrix);
	float3 rayLS = mul(reflectVect, (float3x3) obbMatrix);

	float3 firstPlaneIntersect = (float3(1.0f, 1.0f, 1.0f) - positionLS) / rayLS;
	float3 secondPlaneIntersect = (-positionLS) / rayLS;
	float3 furthestPlane = max(firstPlaneIntersect, secondPlaneIntersect);
	float dist = min(furthestPlane.x, min(furthestPlane.y, furthestPlane.z));

	// Use distance in WS directly to recover intersection
	float3 intersectPositionWS = In.vWorldPos + reflectVect * dist;
	reflectVect = intersectPositionWS - cubemapPos;
#endif

	float3 flCubemap = ENV_MAP_SCALE * texCUBE( sEnvmap, reflectVect ).rgb;
	flCubemap *= flSpecularFactor;
	flCubemap *= g_flEnvmapTint;

#if ENVMAPFRESNEL
	flCubemap *= flFresnel;
#endif

	float3 specularLightingSquared = flCubemap * flCubemap;
	flCubemap = lerp( flCubemap, specularLightingSquared, g_flEnvmapSaturationContrast.y );
	float3 greyScale = dot( flCubemap, float3( 0.299f, 0.587f, 0.114f ) );
	flCubemap = lerp( greyScale, flCubemap, g_flEnvmapSaturationContrast.x );

	flSpecularLighting += flCubemap;
#endif

#if SELFILLUM
	float flSelfIllumAmount;
#if SELFILLUM_MASK
	flSelfIllumAmount = tex2D( sSelfIllumMask, In.vTexCoord.xy ).r;
#elif SELFILLUM_ENVMAP_ALPHA
	flSelfIllumAmount = vecEnvmapmaskColor.a;
#else
	flSelfIllumAmount = flAlbedoSample.a;
#endif
	flLighting.rgb = max( flLighting.rgb,
		flAlbedoSample.rgb * g_flSelfIllum_Tint * flSelfIllumAmount );
#endif

	float flLightScale =
#if DEFCFG_USE_SRGB_CONVERSION
		LINEAR_LIGHT_SCALE;
#else
		GAMMA_LIGHT_SCALE;
#endif

	Out.vColor_0.xyz = ( ( flAlbedoSample + flLighting.w * g_flPhongScale ) * flLighting.xyz
		+ flSpecularLighting ) * flLightScale;

#if PIXELFOGTYPE == 0
	float fogAmt = CalcPixelFogFactor( PIXELFOGTYPE, g_vecFogParams, g_vecOrigin, In.vWorldPos, 0 );

	Out.vColor_0.xyz = lerp( Out.vColor_0.xyz, g_LinearFogColor, fogAmt );
#endif

	return Out;
}