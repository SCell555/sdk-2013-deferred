
// STATIC:		"ALPHATEST"			"0..1"
// STATIC:		"BUMPMAP"			"0..2"
// STATIC:		"NOCULL"			"0..1"
// STATIC:		"PHONGMAP"			"0..1"
// STATIC:		"BUMPMAP2"			"0..1"
// STATIC:		"BLENDMODULATE"		"0..1"

// SKIP:		!$BUMPMAP2 && $BLENDMODULATE
// SKIP:		!$BUMPMAP && $BUMPMAP2
// SKIP:		$MODEL && $BUMPMAP2

// SKIP:		$DECAL && $BLENDMODULATE
// SKIP:		$DECAL && $BUMPMAP
// SKIP:		$DECAL && $NOCULL
// SKIP:		$DECAL && $PHONGMAP

#include "common_ps_fxc.h"
#include "common_deferred_fxc.h"


#if ALPHATEST
sampler sAlbedo							: register( s0 );
#endif

#if ALPHATEST
const float g_flAlphaRef				: register( c0 );
#endif


#if BUMPMAP
sampler sBumpmap						: register( s1 );
#endif

#if PHONGMAP
sampler sPhongmap						: register( s2 );
#else
const float2 g_flPhong_exp				: register( c2 );
#endif

const float2 g_flHalflambert_Litface	: register( c1 );

#if BUMPMAP2
sampler sBumpmap2							: register( s3 );
#endif

#if BLENDMODULATE
sampler sBlendmodulate						: register( s4 );
#endif

//sampler sControlTexture						: register( s5 ); // r = metalness, b = roughness
//sampler sControlTexture2					: register( s6 ); // r = metalness, b = roughness

struct PS_INPUT
{
#if BLENDMODULATE
	float4 vTexCoord				: TEXCOORD0;
#else
	float2 vTexCoord				: TEXCOORD0;
#endif
	float4 worldNormal_Depth		: TEXCOORD1;

#if BUMPMAP
	float3 worldTangentS			: TEXCOORD2;
	float3 worldTangentT			: TEXCOORD3;
#endif

	float3 worldPos					: TEXCOORD4;

#if NOCULL
	float vDir						: VFACE;
#endif

#if BUMPMAP2
	float4 vColor_0					: COLOR0;
#endif
};


struct PS_OUTPUT
{
	float4 vColor_0				:	COLOR0;

#if ( DECAL == 0 )
	float4 vColor_1				:	COLOR1;

	float4 vColor_2				:	COLOR2;
	
	float4 vColor_3				:	COLOR3;
#endif
};


PS_OUTPUT main( const PS_INPUT In )
{
	PS_OUTPUT Out;

#if ALPHATEST
	float4 flAlbedoSample = tex2D( sAlbedo, In.vTexCoord.xy );
#	if ALPHATEST
	clip( flAlbedoSample.a - g_flAlphaRef );
#	endif
#endif

#if DECAL
	Out.vColor_0 = flAlbedoSample;
#else

#if BUMPMAP
	float3 bumpNormal = tex2D( sBumpmap, In.vTexCoord.xy ).rgb;

#if BUMPMAP2
	float3 bumpNormal2 = tex2D( sBumpmap2, In.vTexCoord.xy ).rgb;
	float flBlendfactor = In.vColor_0.w;
#if BLENDMODULATE
	flBlendfactor = GetModulatedBlend( flBlendfactor, tex2D( sBlendmodulate, In.vTexCoord.zw ).rg );
#endif
	bumpNormal = lerp( bumpNormal, bumpNormal2, flBlendfactor );
#endif

	float3 tangentNormal;
#if BUMPMAP == 1
	tangentNormal = bumpNormal * 2.0f - 1.0f;
#else
	tangentNormal = normalize( bumpBasis[0]*bumpNormal.x + bumpBasis[1]*bumpNormal.y + bumpBasis[2]*bumpNormal.z);
#endif
	float3 worldNormal = Vec3TangentToWorld( tangentNormal, In.worldNormal_Depth.xyz, In.worldTangentS, In.worldTangentT );
#else
	float3 worldNormal = In.worldNormal_Depth.xyz;
#endif

#if NOCULL
	worldNormal *= In.vDir;
#endif

	worldNormal = worldNormal * 0.5f + 0.5f;

#if ( TRANSLUCENT == 0 )
#	if PHONGMAP
	float phong_exp = tex2D( sPhongmap, In.vTexCoord.xy ).x * 63.0f;
#	else
#		if BUMPMAP2
		float phong_exp = lerp( g_flPhong_exp.x, g_flPhong_exp.y, flBlendfactor );
#		else
		float phong_exp = g_flPhong_exp.x;
#		endif
#	endif
#endif

	Out.vColor_0 = float4( worldNormal, 1 );
	Out.vColor_1 = float4( In.worldNormal_Depth.w, 0, 0, 1 );
	Out.vColor_2 = float4( phong_exp, g_flHalflambert_Litface.x, g_flHalflambert_Litface.y , 0 );
	Out.vColor_3 = float4( In.worldPos, In.worldNormal_Depth.w );
#endif

	return Out;
}